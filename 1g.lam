let
 true = \a b.a;
 false = \a b.b;
 is_zero = \n . n (\x.false) (true);
 if = \b e1 e2. b e1 e2;
 not = \b. if b false true;
 

 even = \n. n  (not) (true);
 pair = \a b. \c. c a b;
 fst = \p. p true;
 snd = \p. p false;
 succ = \n. (\a. \b. a (n a b));
 add = \n1 n2. (\a b. n1 a (n2 a b));
 mult = \n1 n2. (\a b. n1 (n2 a) b);
 shift_inc = \p. pair (snd p) (succ (snd p));
 
 pred = \n. fst (n shift_inc (pair 0 0));
 sub = \n1 n2. n2 pred n1;
 menor = \n1 n2. not(is_zero(sub n2 n1));
 maior = \n1 n2. is_zero(sub n2 n1); 

 Y = \f. (\x. f(x x)) (\x. f(x x));
 div3 = Y (\r. \a. if(menor a 3)  0 (succ(r (sub a 3))));

 empty = \x. true;
 cons = pair;

 isEmpty = \l. l (\a b. false);
 head = fst;
 tail = snd;
 soma_lista = Y (\r.\l. if(isEmpty l) 0 (add (head l) (r (tail l))));
 mult_lista = Y (\r.\l. if(isEmpty l) 1 (mult (head l) (r (tail l))));
 
in
 mult_lista(cons 2(cons 5 (cons 3 empty)))